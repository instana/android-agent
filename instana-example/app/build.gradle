apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.akaita.android.easylauncher'
apply plugin: 'com.instana.android-agent-plugin'

android {
    compileSdkVersion 30

    defaultConfig {
        targetSdkVersion 30
        versionName "5.0"
        versionCode 1
        vectorDrawables.useSupportLibrary = true

        def instanaProperties = new Properties()
        instanaProperties.load(new FileInputStream("$project.projectDir/instana.properties"))
        buildConfigField 'String', 'INSTANA_KEY', "${instanaProperties["instana.key"]}"
        buildConfigField 'String', 'INSTANA_REPORTING_URL', "${instanaProperties["instana.reportingURL"]}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        debug {
            storeFile file("$project.projectDir/keys/debug.keystore")
            storePassword 'password'
            keyAlias = 'debug'
            keyPassword 'password'
        }
        release {
            storeFile file("$project.projectDir/keys/release.keystore")
            storePassword 'password'
            keyAlias = 'release'
            keyPassword 'password'
        }
    }
    flavorDimensions "default"
    productFlavors {
        api21 {
            minSdkVersion 21
        }
        api16 {
            minSdkVersion 16
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions {
        animationsDisabled true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation project(path: ':network')

    // Language and support
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'android.arch.work:work-runtime-ktx:1.0.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.3'

    // Network clients
    api21Implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    api16Implementation 'com.squareup.retrofit2:retrofit:2.6.4'

    // Instana Agent
    implementation "com.instana:android-agent-runtime:$agent_version"

    // Check for Google Play Services availability
    implementation 'com.google.android.gms:play-services-basement:17.6.0'
    implementation 'com.google.android.gms:play-services-base:17.6.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
}

// Apply a different ribbon to the launcher of each app build type
easylauncher {
    foregroundIconNames "@mipmap/ic_launcher_foreground"
    buildTypes {
        debug {
            filters = greenRibbonFilter()
        }
        release {
            filters = blueRibbonFilter()
        }
    }
}
