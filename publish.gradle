apply plugin: "io.github.gradle-nexus.publish-plugin"

// root publish.gradle (CONFIG ONLY)
ext.lib_group_id = "com.instana"

// Read credentials from publish.properties created by CI
def props = new Properties()
def propsFile = new File("$rootDir/publish.properties")
if (propsFile.exists()) {
    props.load(new FileInputStream(propsFile))
} else {
    logger.warn("publish.properties not found; release tasks will be no-op.")
}

// Backwards-compatible keys: prefer portal.*, fallback to ossrh.*
def portalUser = (props['authentication.portal.username'] ?: props['authentication.ossrh.username']) ?: ''
def portalPass = (props['authentication.portal.password'] ?: props['authentication.ossrh.password']) ?: ''

nexusPublishing {
    repositories {
        sonatype {
            username.set(portalUser)
            password.set(portalPass)
        }
    }
}

// Keep pipeline command unchanged by aliasing the old task name
tasks.register("closeAndReleaseRepository") {
    dependsOn("closeAndReleaseSonatypeStagingRepository")
}
